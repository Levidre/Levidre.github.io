//---------------------------Menus--------------------------
//This .dic file is primarily concerned with the double-click menus for each of your ghosts. If you don't want to have menus for your characters, you'd just have to delete the OnMouseDoubleClick functions near the end of mouse.dic and then ignore this file, as none of it will be called I believe. Menus can be cool though, since you can put stuff in them and create new functions if you know what you're doing. For example, I've added a lot of games and interactions and little linked icons to my Hunter Smoker ghost to keep track of their feelings and such. Much of that is pretty complicated though (and kind of messy, cough), so I won't go into it here, but I covered some of the concepts in the coding page of the walkthrough, and enterprising ghost makers can study the menu.dic file I set up for Hunter/Smoker and see how I put it together.
//Anyway, menu.dic also contains the settings for a config menu that will let your user change their name or pronouns if they want, as well as the talk rate of the ghost, all of which are useful things for your ghost to do. So think hard about whether or not you want to skip this file or not.

//The complexity of this file depends on how customized you want your menus. If you just want to leave them as they are, then all you'll have to do is replace dialogue appropriately. You can always add stuff to their menus later if you get the urge. Otherwise, if you have plans in mind, you'll need to study what's going on in here carefully and pay attention to how things are set up! You can do a lot of unique stuff in this file if you're creative.

//Make sure to read the walkthrough (http://www.ashido.com/ukagaka/walkthrough.html) for more details about how to properly format ghost dialogue!
//I will reiterate that you should be editing these in Notepad++, and in particular, you should set the Language to C (or Java I guess) to properly highlight all the text. It will make things A LOT EASIER for you, trust me! It will also help you keep track of your brackets in case you forget some. If you click on a line with a bracket in Notepad++, it should highlight where the other bracket ends, or turn purple if there's no finishing bracket (which you should definitely fix!)
//It will also highlight my commented lines in green, so you'll know where they are. Any line beginning with // is a commented line that the code will not read. It's just for your reference!


//--OpenSakuraMenu
//This is a bit of intro dialogue before the main character opens their menu. You can see below that there are three pieces of separate dialogue listed before it goes to MenuRun. The character will randomly choose one of those pieces of dialogue before opening the rest of their menu. You can customize this with individual greetings for the character, such as "Yes, %(username)?" or "Need something, %(username)?" or things like that. Note that none of the lines end with \e!
//The \b2 tag tells the ghost to use their big balloon for this dialogue. If your menu has a lot of stuff, this'll probably look better than the small one.

OpenSakuraMenu
{

	"\0\b2\i[92]"
	--
	"\0Yeah?"
	"\0Mmh?"
	"\0What do you want?"
	"\0What?"
	--
	MenuRun
}

//--MenuRun
//As you just saw above. This actually fills out the rest of the menu. If you're going to be adding or changing functions, this is where you'll be doing your edits.

MenuRun
 {	
 	wd = GetWeekDay
 	ap = GetAP
 	nowmin = Getnowmin
 	nowhour = Getnowhour
	//These four statements basically get the day of the week, whether it's AM or PM, the hour, and the time. It's putting together the information to display the time at the bottom of the balloon. Pay it no mind.
	--
	"\n\n\i[2001]\_q\b2" //The \n\n means two new lines as detailed in the walkthrough, and \_q means quick session, meaning all the text from this point on will display instantly instead of a few letters at a time like normal ghost dialogue.
	--
	
	"(You may want him to...)\n\n[half]"
	--
	if lastTalk != "" //What this does is check to see if the ghost has already said a random bit of dialogue on its own, essentially by seeing if the lastTalk thing is NOT empty (!= means "does not equal").
	{
	  "\![*]\q[Repeat Last Dialogue,OnLastTalk]\n"
	  //If the ghost has said something, it will then display this menu option. You can replace "Repeat Last Dialogue" with whatever caption you like, such as "What were you saying?" or "What did you say?" or "Could you repeat that?" or something like that.
	  
	  //*********************Advanced User Info*******************************
	  //These text boxes are meant for those who have an interest in expanding or using more complicated functions in their ghost. Also for my own reference in the future since I plan on using this template too, haha. If you have no interest in any of this and just want to keep things as simple as possible, feel free to skip these boxes.
	  
	  //I briefly touched in bootend.dic about \q, but I'll again cover it here. Pay very close attention to how this bit of dialogue is formatted. You'll see it goes \![*]\q[Displayed Name,linkedfunction]. Breaking it down, \![*] puts the balloon's option icon in front of the choice (usually a > or something), \q sets up the choice, Displayed Name which is the text that will display in the ghost's balloon, then linkedfunction, which is what the choice is linked to when the user clicks it. As you go through this file, look at how every menu option is set up, and you will see they all follow this format, give or take a \![*]. If you're going to be adding new options to menus or creating your own functions and such, make sure you follow this format and learn it well. Make sure to read the coding page of the walkthrough.
	  //***********************************************************************
	}
	--
 	"\![*]\q[Talk to you,altalk]"	
	--
	"\n\n[half](You may want to interact with him...)\n\n[half]"
	--
	"\![*]\q[Some questions,askzimquestions]\n\![*]\q[Do something,actzimmenu]\n"
	--
	if displaytest == 1
	{
		"\n[half](The test functions and some useful things)\n\n[half]"
		--
		"\![*]\q[Call the test function,testfunction]\n"
		--
		"\![*]\q[Saori test menu,SaoriTestMenu]\n"
		--
		"\![*]\q[Explain how loops work,loopmenu]\n"
		--
		"\![*]\q[Call the second test function,othertestfunction]\n"
		--
		"\![*]\q[Display some variables,displayvariables]\n"
		--
		"\![*]\q[Reinitilize some variables,reinivariables]\n"
		--
		"\![*]\q[Find a word in the vocabulary,findvocabulary]\n"
	}
	--
	"\n[half](Or he could do something for you...)\n\n[half]\![*]\q[Config Menu,configmenu]    /
	\![*]\q[Functions,zimdosomething]\n\n[half]/
 	\![*]\q[Nevermind,CANCEL]\n\n\n[half]%(wd) %(nowhour):%(nowmin) %(ap), %(month)/%(day)/%(year)"
	--
	"\e"
	--
	SAVEVAR
	//These three lead to the Config Menu, the Function menu, and Cancel. You can leave these alone if you like. All the envelopes at the end (see word.dic) basically display the year, month, day, weekday, and time.
}

//Would run whatever you want.
Select.testfunction
{
	/*intervalslist =IARRAY
	surfaceslist= IARRAY
	ParseShell("\master")
	Select.scanShellMenu*/
	//"hello\e\s[20]"
}

Select.othertestfunction
{
}

Select.displayvariables
{
	
	SAVEVAR
	--
	"\e"
}

//It would reinitialize some variables then display the menu.
Select.reinivariables
{
	zimpunched = 0
	discovered = 1
	
	if findgircute
	{
		findgircute = 0
	}
	else
	{
		findgircute = 1
	}
	
	
	if discovered == 1
	{
		discovered = 0
	}
	else
	{
		discovered = 1
	}
	
	MenuRun
}

OnVoiceRecognitionWord
{
	_wordsayed=reference1
}

Select.close
{
	"\![leave,passivemode]\e"
}

//The menu for the explanations of loops
Select.loopmenu
{
	"%(lsto)* Which loop do you want me to explain ?\n\n[half]\_q"
	--
	"\![*]\q[A foreach one,expforeach]    \![*]\q[A while one,expwhile]"
}
Select.expforeach
{
	//Create the array
	myarray = IARRAY
	myarray[0] = "potatoes,chips"
	myarray[1] = "tomato,ketchup"
	myarray[2] = "bread,pie"
	--
	"\b2%(lsto)* I defined an array, it looks like :\n'' potatoes , chips ''\n'' tomato , ketchup ''\n'' bread , pie ''"
	--
	//Initialize the index to 0
	index = 0
	--
	"\n\n[half]* I start the foreach loop, the index is 0.\n"
	--
	//Initialize what will be displayed to an empty string, a chain of characters
	explainationsdisplayed = ""
	--
	"* There is a little problem with a foreach loop,\w5 if I try to display something during the loops,\w5 it would only display one randomly.\w7 So I stock each loop the dialogue I should have displayed in a big chain of characters I display after the foreach loops.\n\n[half] "
	--
	//The foreach, finally, line becomes each line of myarray one after the other
	foreach myarray ; line
	{	
		--
		//Each loop, the dialogue of what Azura does is added to what would be displayed at the end
		explainationsdisplayed = explainationsdisplayed + "* A loop : for now, line is %(line), the first element of line is %(line[0]) and the index of line in myarray is %(index)\n"
		--
		if index == 0
		{
			explainationsdisplayed = explainationsdisplayed + "* It was the first loop.\n"
		}
		--
		index = index + 1
		--
		if index == ARRAYSIZE(myarray)
		{
			explainationsdisplayed = explainationsdisplayed + "* It was the last loop, index is now equal to the size of myarray."
		}
	}
	--
	//Now, Azura display what she did during the loops
	"%(explainationsdisplayed)\x\e"
}
Select.expwhile
{
	"\b2%(lsto)* A little explanation is a necessity for those.\w7 While loops are very versatile but dangerous too. \w7They are the most dangerous loops that exist because they can be infinite. \w7That means they can run forever without ending, \w5so it could be assimilate to a crash. \w7While loops run while a condition is true and stop when it become false, \w5a condition that never become false will make the loops run forever.\n"
	--
	"* So what looks like this while loops ? \w7Something easy, \w5it looks like if-else statements. \w7It's like if the condition is true, \w5continue the loops. \w7Normally, a if statement runs once, \w5but a while will restart the loop until it leads to an else.\n"
	--
	"* For what using while loops ? Well, \w5what you want, \w5I'm myself using it to read, load or save files.\n\n[half]"
	--
	"\f[italic,true]You have the feeling a good example is in the function under this one or there is a little more complicated one in commu.dic. The function under this one isn't called by anything yet but it does a list of all the ascii code (the alphabet in computer language) used by ssp, it doesn't do anything else and isn't used by anything, but it was fun to do."
	--
	"\nThe one in commu.dic has a real utility, it serves for the interaction with other ghosts. What should do Azura is looking into the saving file of the other ghost to find a certain variable to know if the ghost is speaking or not. It's not finished yet though, it still checks the wrong variable.\f[italic,false]\n\n[half]"
	--
	"* I will do a very simple while loop,\w5 the condition will be a number under ten, \w5when this number would be superior than 10,\w5 the loops will stop and I display the number.\n\n[half]"
	--
	"* So I initialyze the number nb to 0 before starting the loops. \w7Each loop, \w5I increment nb of 1.\n"
	--
	nb = 0
	while nb <= 10
	{
		nb = nb + 1
	}
	--
	"\w8* Done, \w5and now nb is %(nb).\x\e"
	
}

Select.altalk
{
	AiTalk
}

//Leave this alone
OnLastTalk 
{
"%(lastTalk)\e"
}

//This is figuring out what day of the week it is. Leave it alone.
GetWeekDay
{
	if weekday == 0
	{"Sun"}
	elseif weekday == 1
	{"Mon"}
	elseif weekday == 2
	{"Tue"}
	elseif weekday == 3
	{"Wed"}
	elseif weekday == 4
	{"Thu"}
	elseif weekday == 5
	{"Fri"}
	else
	{"Sat"}
}

//This is finding out whether it's AM or PM. Leave it alone.
GetAP
{
 	if hour >= 0 && hour <= 11
 	{
 		"AM"
 	}
 	else
 	{
 		"PM"
 	}
}
//This is figuring out the minute. Leave it alone.
Getnowmin
{
	if minute >= 0 && minute <=9
	{
		"0%(minute)"
	}
	else
	{
		"%(minute)"
	}
}
//This is figuring out the hour. Leave it alone.
Getnowhour
{
	if hour12 >= 0 && hour12 <=9
		{
			"0%(hour12)"
		}
	else
		{
			"%(hour12)"
		}
}

Select.BACKTOMENU
{
	MenuRun
}

Select.configmenu
{
	TalkConfig
}

//--TalkConfig

TalkConfig
{
	"\0\b[2]\i[2001]\_q(Your settings :)\n\n[half]"
	--
	"\![*]\q[Change your name,TEACHNAME] \n        \f[italic,true](currently %(username))\f[italic,false]\n/
	\![*]\q[Change your pronouns,TEACHPRONOUN] \n        \f[italic,true](currently %(heshe)/%(himher)/%(hisher))\f[italic,false]\n/
	\![*]\q[Change your birthday,TEACHBDAY] \n        \f[italic,true](currently %(birthdayprint))\f[italic,false]\n/
	\![*]\q[Change the talk rate,talkinter] \n        \f[italic,true](currently %(talktime))\f[italic,false]\n"
	--
	"\f[italic,false]\n\![*]\q[Enable Uninstall,deleteonoff] \n        \f[italic,true](currently %(deleteshitai))\f[italic,false]\n/
	\![*]\q[Open SSP preferences,sspconfig]\n\n[half]"
	--
	"	\![*]\q[Enable the tests,enabletest]\n\n[half]/
	\![*]\q[Back to the menu,BACKTOMENU]\e"
	
	
}

Select.emailsettings
{
	"\![open,configurationdialog]"
}

Select.enabletest
{
	if displaytest == 1
	{
		
		--
		displaytest = 0
	}
	else
	{
		--
		displaytest = 1
	}
	--
	"\e"
}

//This and the next few set up the pronouns, as you can see by the linkedfunction names. You can replace "\0\s[0]What pronouns do you want?" with your own dialogue if you prefer, but leave the rest of it alone.
Select.TEACHPRONOUN
{
	if asleep != 1
	{
		"\0%(lsto)* What pronouns do you want?\w5\n\n[half]\_q/
		\![*]\q[He-His-Him,choicemasc]\n/
		\![*]\q[She-Her-Her,choicefem]\n/	
		\![*]\q[They-Their-Them,choiceneither]\e"
	}
	else
	{
		--
		"\f[italic,false]\x\![raise,OnChoiceSelect,configmenu]"
	}
}

Select.choicemasc
{
	presuffix = "masculine"
	himher = "him"
	heshe = "he"
	hisher = "his"
	--
	"%(lsto)* I will use he, him and his.\e"
	//Leave the part above the double dashes alone. You can replace the snip of dialogue however with whatever you like.
}
Select.choicefem
{
	presuffix = "feminine"
	himher = "her"
	heshe = "she"
	hisher = "her"
	--
	"%(lsto)* I will use she, her and her.\e"
	//as above, you can also replace this dialogue.
}
Select.choiceneither
{
	presuffix = "undefine"
	himher = "them"
	heshe = "they"
	hisher = "their"
	--
	"%(lsto)* I will use they, them and their.\e"
	//and this one.
}


//These two options toggle whether or not you can delete your ghost. Leave them alone.

Select.deleteonoff
{
	if deleteshitai == "OFF"
	{
		deleteshitai = "ON"
	}
	else
	{
		deleteshitai = "OFF"
	}
	--
	TalkConfig
}
Select.deleteoff
{
	deleteshitai = "OFF"
	TalkConfig
}


//This next set of functions handles inputting the user's birthday.

Select.TEACHBDAY
{
	dayslot = GetDaySlot //This is setting up in case today is the user's birthday.
	"\f[italic,false]\x\![raise,OnChoiceSelect,configmenu]"

	//You can replace the dialogue up to the \![open,inputbox,OnUserBornMonth,-1] tag, leave that alone. You'll need the input box to get the information. Note that the month HAS to be put in in number format as well, so you might want to have your ghost mention that.
}

//--OnUserBornMonth
//This takes the value that the user put into the input box above and evaluates it.

OnUserBornMonth
{
	_tempmonth = reference0 //this stores what the user input in as a variable called _tempmonth
	if _tempmonth >= 1 && _tempmonth <= 12 //if the month is a valid month
	{
		bornmonth = _tempmonth
		bornmonthprint = Printmonth
		
		"%(lsto)* So you were born in %(bornmonthprint).\n\w8* What day was it?\![open,inputbox,OnUserBornDay,-1]\e"
		
		//Here you can see the use of a bornmonthprint envelope to show the user what month they put in. Notice that it opens an inputbox at the end the same way the one above did. You can replace the dialogue up to the input box just as with above.
	}
	else //if the month isn't a valid month, like 32 or something.
	{
		"%(lsto)* That's not a valid month. Try again.\![open,inputbox,OnUserBornMonth,-1]\e"
		
		//You can replace this dialogue as well up to the \! tag, as mentioned. You need to leave the tag for the input box so they can put in a correct month.
	}
}

//This puts the name to the number of the month. No need to mess with these.
Printmonth
{
	if bornmonth == 1
	{
		"January"
	}
	elseif bornmonth == 2
	{
		"February"
	}
	elseif bornmonth == 3
	{
		"March"
	}
	elseif bornmonth == 4
	{
		"April"
	}
	elseif bornmonth == 5
	{
		"May"
	}
	elseif bornmonth == 6
	{
		"June"
	}
	elseif bornmonth == 7
	{
		"July"
	}
	elseif bornmonth == 8
	{
		"August"
	}
	elseif bornmonth == 9
	{
		"September"
	}
	elseif bornmonth == 10
	{
		"October"
	}
	elseif bornmonth == 11
	{
		"November"
	}
	else
	{
		"December"
	}
}

//This adds an appropriate suffix to the number for the day. No need to mess with this either.

Printday
{
	if bornday == 1 || bornday == 21 || bornday == 31
	{
	"%(bornday)st"
	}
	elseif bornday == 2 || bornday == 22
	{
	"%(bornday)nd"
	}
	elseif bornday == 3 || bornday == 23
	{
	"%(bornday)rd"
	}
	else
	{
	"%(bornday)th"
	}
}	

//--OnUserBornDay
//This is much like the above function OnUserBornMonth. It'll take the value the user put in for the day and analyze it.

OnUserBornDay
{
	tempday = reference0 //storing the input in tempday
	
	if bornmonth == 1 || bornmonth == 3 || bornmonth == 5 || bornmonth == 7 || bornmonth == 8 || bornmonth == 10 || bornmonth == 12 //This is checking to see if it's one of the months with 31 days.
		{
			if tempday >= 1 && tempday <= 31 //if it falls in the right range
				{
					bornday = tempday
					borndayprint = Printday
					userbirthday = "%(bornmonth)month %(bornday)day"
					birthdayprint = "%(bornmonthprint) %(borndayprint)"
					
					"%(birthchecked)"
					//Mostly defining envelopes and values in here. No need to touch any of this! Eagle-eyed readers will notice the new envelope birthchecked on its own line like a tiny king. We'll get to that momentarily, but pay it no mind for now.
				}
			else //if the day isn't valid, like the 10046th or something.
				{
					"\0%(lsto)* That's not a valid day. Try again.\![open,inputbox,OnUserBornDay,-1]\e"
					
					//Similar as you can see to the month check above. You can replace the dialogue here up to the tag as you just did, but make sure to leave the input box tag so the user can put in a correct day.
				}
		}
	elseif bornmonth == 2 //if the month is february
		{
			if tempday >= 1 && tempday <= 29 //and the day falls in the right range
				{
					bornday = tempday
					borndayprint = Printday
					userbirthday = "%(bornmonth)month %(bornday)day"
					birthdayprint = "%(bornmonthprint) %(borndayprint)"
					
					"%(birthchecked)"
					//just as with the above.
				}
			else
				{
					"\0%(lsto)* That's not a valid day. Try again.\![open,inputbox,OnUserBornDay,-1]\e"
					
					//And the same as above again! You see the pattern here?
				}
		}
	else //if it's any other month than those
		{
			if tempday >= 1 && tempday <= 30 //if the day falls between these values
				{
					bornday = tempday
					borndayprint = Printday
					userbirthday = "%(bornmonth)month %(bornday)day"
					birthdayprint = "%(bornmonthprint) %(borndayprint)"
					
					"%(birthchecked)"
				}
			else
				{
					"\0%(lsto)That's not a valid day. Try again.\![open,inputbox,OnUserBornDay,-1]\e"
				}
		}		
}

//--birthchecked
//This is the new envelope you saw me point out above. As you can see here, you can actually call dialogue in a function with an envelope, even when it has if and else statements like here! Which can be pretty handy. Again, more on envelopes in word.dic. Creative ghostmakers can find handy ways to exploit this, I'm sure.

birthchecked
{
	if userbirthday == dayslot //if today is the user's birthday
		{
			"\0It's your birthday today.\w7 Happy birthday, %(username).\e"
			//replace this dialogue with whatever you like
		}
	elseif userbirthday == "12month 25day"
		{
			"\0* You were born on Christmas. \w7It's sad to have presents only once per year.\e"
			//As you can see, you can set up an elseif statement to check if the user's birthday is any day you want! More on elseif statements in bootend.dic. In this case, there's specialized dialogue if the user was born on Christmas. You can replace this with whatever you like, or change the date you're checking as well, like to one of your character's birthdays if you want. You can also delete this elseif if you don't care.
		}
	else //if the birthday is not listed above.
		{
			"\0* Your birthday is %(bornmonthprint) %(borndayprint).\w7 I'll remember.\e"
			//note the use of envelopes to display the newly set birthday properly. You can replace this dialogue as well, and you can use %(bornmonthprint) and %(borndayprint) to display the month and day in the new dialogue, if you want.
		}
}

//--Talk Rate
//This is when the user chooses to adjust the talk rate in the config menu. The talk rate is how often the ghost will read some dialogue from aitalk.dic.

Select.talkinter
 {
	if asleep != 1
	{
		"\0\b2\i[2001](How often do you want him to speak ?\w7 For now he speaks every %(talktime).)\n\n[half]/
		//note the new envelope here for %(talktime). You'll see it again in a second. You can replace the dialogue above with whatever you like, just make sure it ends with ./ and not \e.
		\![*]\q[Every 30 seconds,TALKMAX]\n/
		\![*]\q[Every minute,TALKHIGH]\n/
		\![*]\q[Every 3 minutes,TALKNORMAL]\n/
		\![*]\q[Every 5 minutes,TALKPOOR]\n/
		\![*]\q[Don't speak,TALKNONE]\n/
		\![*]\q[No change,TalksTill]\e"
	}
	else
	{
	}
}

	
//If the user doesn't want to change the rate. You can change all of the dialogue in any of these options if you want to reflect the choice the user made. You TECHNICALLY could also change the value in talktime to reflect some other way of measuring time but I would NOT recommend it, it'd just be confusing I'm sure.
Select.TalksTill
{
	"\i[2001](He doesn't react.)\e"
}

//If the user chooses every 30 seconds.
Select.TALKMAX
{
	talktime = "30 seconds" //this sets up the envelope properly
	aitalkinterval = 45 //this changes the actual rate	
	"%(zsatisfied)Hehehe, \w5you will hear more and more of me.\e"
}
Select.TALKHIGH
{
	talktime = "minute"
	aitalkinterval = 90
	"\0%(zpose)%(zsatisfied)I will speak every minute, \w5what a very good rhythme.\e"
}
Select.TALKNORMAL
{
	talktime = "3 minutes"
	aitalkinterval = 270
	"\0%(zpose)%(zboredeyes)%(zwonderingarms)%(zsmile)\i{[101]I will speak every three minutes.\w7 %(zrelaxedarms)That's a good rhythme.\e"
}
Select.TALKPOOR
{
	talktime = "5 minutes"
	aitalkinterval = 400
	"%(zboredeyes)\i[101]Pffff,\w5 you won't hear a lot from me.\e"
}

//If the user chooses to not have them speak. This is one case where you MIGHT want to make talktime equal something else, if you have a snappier way of saying the ghost won't talk. Make sure it flows well in your menus though, since it will be displayed in your config menu.
Select.TALKNONE
{
	talktime = "...actually he doesn't talk at all"
	aitalkinterval = 0
	"%(zangryeyes)%(zsmile)%(zrelaxedarms)How dare you telling me to stop talking.\e"
}

	
//This is when the user chooses to change their name. It's linked to the functions outlined in nameteach.dic. Nothing to touch here.
Select.TEACHNAME
{
	OnTeachName
}

	
//If the user selects cancel in a menu. Replace as you like, or just leave them blank.
Select.CANCEL
{
	
}
	
//Going back to the MenuRun function we finished above, this is if someone selects the Functions option, linked to dosomething. As you can see, it ends up as Select.dosomething.

Select.zimdosomething
{
	if discovered != 1 || useraffection > 175
	{
 		"\0\b2\i[92]"
		--
		"Errr,\w5 yeah?"
		"What do you want?"
		"Say always."
		--
		"\n\n[half]"
		
		//I mentioned this above in MenuRun, but you can have a set of multiple different responses that may randomly appear when the menu is opened if you offset them from the rest with the -- dashes below this note. Try opening this menu yourself with the template to see how it works. The character will randomly choose one line as the intro. 
		//If you don't want to deal with this, just delete the extra lines and leave one. Otherwise, replace the dialogue above as you like, but note that they don't end with \e here. You can leave the rest of this below alone.
		--
		"\![*]\q[Open calendar,zimCALENDAR]\n/
		\![*]\q[Check email,zimMailCheck]\n/
		\![*]\q[Empty recycle bin,zimChoiceGarbagecan]\n/
		\![*]\q[Update Zim,zimupdatecheck]\n"
		--
		"\![*]\q[System information,zimChoiceSystemInformation]\n/
		\![*]\q[Update information,AfterUpdate]\n/
 		\![*]\q[Back to main menu,BACKTOMENU]\e"
	}
	else
	{
		"\0I'm not your servant."
		"\0I won't do anything for a filthy human!"
		"\0Why would I do anything for you?"
	}
}

Select.girdosomething
{
	if discovered != 1
	{
 		"\0\i[92]\i[114]\i[112]\i[129]\i[100]\i[69]\i[130]He is a dog,\w5 he can't do anything for you.\1\i[92]\i[125]\i[126]\i[52]\i[65]Awww...\0\i[143]\i[193]\i[101]\n\n[half]No you can't.\e"
		"\0\i[92]\i[114]\i[112]\i[128]\i[100]\i[69]\i[127]He is a dog,\w5 he can't do anything for you.\1\i[92]\i[125]\i[126]\i[52]\i[65]Aoww...\e"
	}
	else
	{
		"\1\b2\i[92]\i[127]\i[128]\i[66]"
		"\1\b2\i[92]\i[127]\i[128]\i[62]"
		--
		"Yeah?"
		"Whaddoyou want?"
		--
		"\n\n[half]"
		
		//I mentioned this above in MenuRun, but you can have a set of multiple different responses that may randomly appear when the menu is opened if you offset them from the rest with the -- dashes below this note. Try opening this menu yourself with the template to see how it works. The character will randomly choose one line as the intro. 
		//If you don't want to deal with this, just delete the extra lines and leave one. Otherwise, replace the dialogue above as you like, but note that they don't end with \e here. You can leave the rest of this below alone.
		--
		"\![*]\q[Open calendar,girCALENDAR]\n/
		\![*]\q[Check email,girMailCheck]\n/
		\![*]\q[Empty recycle bin,girChoiceGarbagecan]\n/
		\![*]\q[Update Zim,girupdatecheck]\n"
		--
		"\![*]\q[System information,girChoiceSystemInformation]\n/
		\![*]\q[Update information,AfterUpdate]\n/
 		\![*]\q[Back to main menu,BACKTOMENU]\e"
	}
}

//When the user clicks Open Calender. This will open the calender function of SSP, an in-built feature that you don't have to worry about in the least.
Select.zimCALENDAR
{
	"\0\i[92]Err,\w5 let's see.\w7\i[201] There.\![open,calendar]\e"
}
Select.girCALENDAR
{
	"\1\i[92]This?\![open,calendar]\0\i[92]\i[101]Good GIR.\1\i[119]\i[120]\i[66]\e"
}

Select.zimsspconfig
{
	"\0%(zboredeyes)\i[92]Err,\w5 let's see.\w7\i[201] There.\![open,configurationdialog]\e"
}

Select.girsspconfig
{
	"\1\i[92]This?\![open,configurationdialog]\0\i[92]\i[101]Good GIR.\1\i[119]\i[120]\i[66]\e"
}

Select.zimChoiceGarbagecan
{
	_gomicheck = FUNCTIONEX("gomi.dll", "-n");	//Don't touch this line. for those curious, it's referencing a SAORI, which are optional user-made plug-in dlls you can download to enable different functions on your ghost. gomi.dll is one included with this template. All SAORI are in Japanese (maybe Korean or Chinese as well idk) and many have disappeared by now, so don't concern yourself very much with SAORI. It's just some ghost trivia, basically.
	
	if _gomicheck == 0 //If the recycle bin is empty.
	{
		"\0\i[92]\i[68]\i[127]\i[128]\i[143]\i[193]\i[112]\i[114]It's already empty.\e"
		
		//This isn't the usual flat template dialogue, mostly because I wanted to show that this is a good place to use s[-1], which I covered in the walkthrough file. You can have your ghost disappear momentarily like they're going to check your recycle bin, then come back. \c clears the ghost's balloon of text. Replace dialogue as you desire.
	}
	elseif _gomicheck >= 1 //If the recycle bin is full.
	{
		"\0\i[92]\i[68]\i[127]\i[128]\i[143]\i[193]\i[117]\i[118]I won't empty your garbage.\w7\i[98]\i[101] GIR!\w7 Go for it!\w8\1\i[92]\i[153]\i[123]\i[124]Yes master!\w8\w8\s[-1]\w8\w8\w8\w8\w8\![raise,OnGarbagecanEmpty]\w9\w9\w9\n\c%(defaultpose)\0There,\w5 it must be done.\e"}
}


//This is for the SAORI, don't touch it.
OnGarbagecanEmpty
{
	_gomi = FUNCTIONEX("gomi.dll", "-f")
}

//This is when the user chooses System Information. Don't touch any of this either. For those curious though, note that this is done using another SAORI, this time saori_cpuid.dll.
Select.zimChoiceSystemInformation
{
	os_name = FUNCTIONEX("saori_cpuid.dll","os.name");
	os_version = FUNCTIONEX("saori_cpuid.dll","os.version");
	cpu_name = FUNCTIONEX("saori_cpuid.dll","cpu.name");
	cpu_clockex = FUNCTIONEX("saori_cpuid.dll","cpu.clockex");
	"/
	\0\b[2]/
	\_q\f[bold,1]OS:\f[bold,0] %(os_name) %(os_version)\n/
	\f[bold,1]Memory Load:\f[bold,0] %(memoryload)%\n/
	\f[bold,1]Physical Memory:\f[bold,0] %(memoryavailphys) KB\n/
	\f[bold,1]CPU:\f[bold,0] %(cpu_name) %(cpu_clockex)MHz\n/
	\f[bold,1]Ukagaka:\f[bold,0] %property[baseware.version]/
	\e"
}
Select.girChoiceSystemInformation
{
	os_name = FUNCTIONEX("saori_cpuid.dll","os.name");
	os_version = FUNCTIONEX("saori_cpuid.dll","os.version");
	cpu_name = FUNCTIONEX("saori_cpuid.dll","cpu.name");
	cpu_clockex = FUNCTIONEX("saori_cpuid.dll","cpu.clockex");
	"/
	\1\b[2]/
	\_q\f[bold,1]OS:\f[bold,0] %(os_name) %(os_version)\n/
	\f[bold,1]Memory Load:\f[bold,0] %(memoryload)%\n/
	\f[bold,1]Physical Memory:\f[bold,0] %(memoryavailphys) KB\n/
	\f[bold,1]CPU:\f[bold,0] %(cpu_name) %(cpu_clockex)MHz\n/
	\f[bold,1]Ukagaka:\f[bold,0] %property[baseware.version]/
	\e"
}

//This is when the user chooses to check their email. The rest of this function plays out in etc.dic. You can leave this alone.
Select.MailCheck
	{
		"\![biff]\e"
	}


//This is when the user ends their headline session. Headlines are detailed more in etc.dic. Replace dialogue with what strikes you.
Select.HEADLINECANCEL
 	{
 		"\0%(zboredeyes)Finished with headlines.\e"
	}

	
//---- OnChoiceTimeout ---------------------------------------------------------
//If the user is given a menu with choices, but doesn't pick one of the choices, it will eventually time out. At that point, they will say this dialogue. Replace with whatever you like, or leave it blank.

OnChoiceTimeout
 {
	if "\0" _in_ reference0
	{
		"\0Boring human..."
		"\0Pfff."
		"\0(You lost his attention.)"
		"\0You're making me lose my precious time"
	}
	elseif "\1" _in_ reference0
	{
		"\1Aowh."
		"\1(You lost his attention.)"
		"\1(He is too agitated to stay attentive.)"
		"\1Uuh."
	}
 }

Select.sendreport
{
	"\0\i[2001](What kind of report do you want to send?)\_q\n\n[half]"
	--
	"\![*]\q[Send a message or a feedback,sendmessage]\n\n[half]"
	--
	"\![*]\q[Go back,dosomething]     \![*]\q[Nevermind,close]"
	temp = ""
	
}
 
 Select.sendmessage
 {
	 "\0\i[2001](What message do you want to send?)"
	 --
	 "\![open,inputbox,OnSendReport,-1,%(temp)]"
 }
 
 OnSendReport
 {
	 temp = reference0
	 "%(lsto)* Will you want to send this message ?\_q\n\n[half]"
	 --
	 "%(lsta)%(reference0)\f[italic,false]"
	 --
	 "\n\n[half]\![*]\q[Yes\, go ahead,actuallysendreport]\n\![*]\q[I wanna rewrite it,sendmessage]"
	 --
	 "\n\n[half]\![*]\q[Nevermind,close]"
	 
 }
 
 Select.actuallysendreport
 {
	_i = FOPEN("./temp.txt","w")
	_i = FWRITE("./temp.txt",temp)
	_i = FCLOSE("./temp.txt")
	_j = SAORI("filetransfer.dll","upload","https","azura-levidre.000webhostapp.com/file-reception.php","./temp.txt")
	_i = FDEL("./temp.txt")
	//if "%(_j)" == "1"
	//{
		"* It's done."
	/*}
	else
	{
		"* Something's wrong,\w5 are you sure your internet is ok ?"
	}
	--
	"%(_j)"*/
	temp = ""
 }
 
//---- Secondary Character Menu ---------------------------------------------------------
//This section has to do with the menu for the second character. If you don't want your second character to have a menu, you can just delete this whole chunk.

OpenKeroMenu
{
	wd = GetWeekDay
 	ap = GetAP
 	nowmin = Getnowmin
 	nowhour = Getnowhour
	//These four statements basically get the day of the week, whether it's AM or PM, the hour, and the time. It's putting together the information to display the time at the bottom of the balloon. Pay it no mind.
	"\1"
	--
	if discovered == 1
	{
		"Yeah?"
		"Waddayou want?"
		"Mmhyeah?"
	}
	else
	{
		"Whoaf?"
		"Whoof?"
		"Whaof?"
		"Mwhof?"
	}
	--
	"\n\n\_q\b2" //The \n\n means two new lines as detailed in the walkthrough, and \_q means quick session, meaning all the text from this point on will display instantly instead of a few letters at a time like normal ghost dialogue.
	--
	

	--
	"\i[2001](You may want to interact with him...)\n\n[half]"
	--
	"\![*]\q[Some questions,askgirquestions]\n\![*]\q[Do something,actgirmenu]\n"
	--
	"\n[half](Or he could do something for you...)\n\n[half]\![*]\q[Functions,girdosomething]\n\n[half]/
 	\![*]\q[Nevermind,CANCEL]\n\n\n[half]%(wd) %(nowhour):%(nowmin) %(ap), %(month)/%(day)/%(year)"
	--
	"\e"
	--
	SAVEVAR
}


//There are no real set things for the second character to do, really. You can mostly add whatever functions you want to them, their menu is totally free reign. In mine for example, Hunter has some games, a pseudo-FAQ for users, a bug report, an update option, and most importantly, a test variable. A lot of that is fairly complicated though, so I won't get into it here. Advanced Users can pick up Hunter and Smoker and study their menu.dic to see how I did it though, and those who read the coding page of the walkthrough and want to make their own functions can feel free to add them here. 
//For the purposes of this simple template, I'll include two options that may come in handy - an update option and a bug report option, as well as the test variable.

//If you click cancel. Replace dialogue as you want.
Select.keroCANCEL
{
	"\1\s[1]This is menu cancel dialogue.\e"
}


//This is when they select bug report. This is mostly meant to briefly give the user information about you, the creator, and how to report a problem to you. This can be handy if the person forgot where they got their ghost or how to contact you. Add whatever info you like here.
Select.bugreport
{
"\1\s[1]This is the bug report dialogue. \w5Report bugs to youremailaddress@emailprovider.com. \w5We were downloaded from \_a[http://www.whereveryoupostedthem.com/]some place\_a.\e"
}

//When clicked, this will automatically tell the ghost to check for updates. More detail on updates in etc.dic. You can leave this alone.
Select.keroupdatecheck
{
	"\![updatebymyself]\e"
}

 
//--Test Variable --------------------------------------------------------
//I briefly mentioned the test variable in bootend.dic and possibly in etc.dic as well, I'm not sure. What exactly is the test variable? Basically, it's a place you can plug in anything you want to test out. What this means is that you can write some ghost dialogue, then copy and paste it into the test variable dialogue snippet down there, reload your ghost, then choose "test variable" from your second character's menu. Check your dialogue to see if it looks right and if it does, put it back in aitalk, or edit it until it looks right and put it back. While you can use Script Input to test most of your ghost's dialogue, there are some lines it just can't do, and it can't help you test choices. But the test variable can! The dialogue/choices from OnFirstBoot are a prime candidate for testing with this variable.
//You can also use this to test functions as I mentioned above by adding \![raise,function] at the end of it, although you can do this with Script Input as well. Advanced Users, the test variable is also great for testing out any new stuff you may add to your ghost, like mode settings, item values, functions, raising or lowering numbers, what have you.

//It is ALWAYS A GOOD IDEA to test your dialogue! If you won't check it with the test variable, at least test it with Script Input! It'll save you so much trouble!

Select.testvariable
{
	//"\1\s[10]Testing!\e"
}