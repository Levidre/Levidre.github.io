//---------------------------Mouse Functions--------------------------
//As you've probably guessed, this .dic file has to do with mouse functions. This includes the petting feature and what happens when you double click your ghost.
//As a note, this .dic file isn't strictly necessary - I'm pretty sure you can just clear the file and it won't hurt your ghost any, it'll just make it so you can't pet or double click them. And if you're interested in only petting or double clicking one character, all you have to do is delete the chunks of code for the other character you want to be uninvolved. However, I'm assuming most people making ghosts want to make their ghosts pettable and clickable, so this will cover the messages for both.

//Make sure to read the walkthrough page (http://www.ashido.com/ukagaka/walkthrough.html) for more details about how to properly format ghost dialogue!
//I will reiterate that you should be editing these in Notepad++, and in particular, you should set the Language to C (or Java I guess) to properly highlight all the text. It will make things A LOT EASIER for you, trust me! It will also help you keep track of your brackets in case you forget some. If you click on a line with a bracket in Notepad++, it should highlight where the other bracket ends, or turn purple if there's no finishing bracket (which you should definitely fix!)
//It will also highlight my commented lines in green, so you'll know where they are. Any line beginning with // is a commented line that the code will not read. It's just for your reference!


//--OnMouseMove
//This is a bunch of code determining who the mouse is over. Don't touch any of it.

OnMouseMove
 {
  	if nowmode != "QUIZ"
 	{
		//---- reference3 determines the character in question
		if reference3 == 0 //if the character is 0, aka the main character
		{
			//---- it will run the mouse functions for that character
			MouseMoveSakura
		}
		else //if the character isn't 0, then it must be the side character.
		{
			//---- this will run the mouse functions for the side character.
			MouseMoveKero
		}
	}
 }
 
 //--MouseMoveSakura

MouseMoveSakura
 {
	if reference4 == "Head" || reference4 == "Antennas" //ignore
 	{
 		zstroke++ //if you move the mouse over the right areas, the stroke count will go up.
 		if zstroke >= 40 //if the stroke count is above a certain number, the petting dialogue will kick in. You can change this number to make the petting take longer or shorter if you want. FLELEs have it set to 50 for some reason, which makes it take longer than this.
		{
 			zstroke = 0 //resetting the stroke count
			if reference4 == "Head" //do you remember the names of the areas you set up for your collisions in surfaces.txt? This is where they come into play! This is where you'd put in the name of your collision, such as head, face, horns, tail, ears, whatever. As mentioned in surfaces.txt, you can add as many collisions to a pose as you want, as long as you make sure to add them all in here so they can actually be used.
 			{
				"\0\i[100]"
				--
				"\i[40]\i[92]%(zwonderingeyes)What are you doing? "
				""
				--
				"\i[147]\i[197]"
				"\i[150]\i[200]"
				"\i[152]\i[202]"
				--
				"\i[68]\i[96]%(zangryeyes)DON'T TOUCH ME!"
				"\i[42]\i[92]How dare you touch \i[68]\f[italic,true]\i[133]\i[134]\i[144]\i[194]Ziiiimmmmm\f[italic,false]!!!"
				"\i[96]\i[98]%(zangryeyes)DON'T TOUCH ME,\w5\i[144]\i[194] you filthy human.\i[92]"
				zimpets++
				useraffection++
			}
 			elseif reference4 == "Antennas" //Another area set up by the collisions in surfaces.txt. You can expand this list with as many elseifs as you need depending on how many collisions you've set up.
 			{
 				{
					"\i[53]\i[92]\i[133]\i[134]\i[140]\i[90]Hey!!\i[151]\i[201]\i[98]\w7 That's sensible!"
					"\i[53]\i[92]\i[133]\i[134]\i[140]\i[90]Hey!!\i[143]\i[193]\i[98]\w7 That's sensible!"
				}
				"\i[53]\i[92]\i[133]\i[134]Hey!!\i[123]\i[124]\i[143]\i[193]\i[64]\w7 Don't touch that!"
 			}
			

 		}
	}
 	else
	{
 		// If the mouse isn't over the ghost, it'll zero out the stroke value.
		zstroke = 0
 	}
}

 //---- MouseMoveKero -----------------------------------------------------------------

MouseMoveKero
 {
	if reference4 == "Head" || reference4 == "Antennas"
 	{
 		gstroke++
 		if gstroke >= 40
		{
			gstroke = 0
			if reference4 == "Head" //Put in the name of your collision area you set up for your secondary character here.
 			{
				if worried == 0
				{
					"%(zpose)%(zeyes)"
					--
					if findgircute != 1 && girpets >= 50
					{
						"%(zplotting)\i[101]\1"
					}
					else
					{
						"%(zsatisfied)\i[101]\1"
					}
				}
				--
				"\i[117]\i[118]"
				"\i[127]\i[128]"	
				"\i[136]\i[137]"
				--
				"\i[63]\i[92]Aww...\n"
				"\i[62]\i[92]Mmhh...\n"
				--
				if discovered != 1
				{
					if worried == 0
					{
						"\0\i[92]Good totally normal dog."
						"\0\i[92]\i[136]\i[137]Totally normal dog,\w5 isn'it?"
						""
					}
					
				}
				else
				{
					"\1Feels nice.\0\n[half]"
					"\1Yes please.\0\n[half]"
					"\1I like pets.\0\n[half]"
					"\1%(ghappy)Pet me."
					""
					--
					if worried == 0
					{
						if findgircute == 0 && girpets >= 50
						{
							"\w8\0\i[51]\i[92]\i[126]\i[112]\i[114]\i[63]\i[143]\i[199]Mmmhh...\w7\i[100] Do you\w1.\w1.\w1. \w5\i[146]\i[196]find him cute?\n\n\n[half]\![*]\q[Of course,yesgircute]     \![*]\q[Not really,nogircute]"
						}
						elseif findgircute != 1
						{
							"\w8\0\i[92]Don't pet him too much.\w7 He will get used to it."
							"\w8\0\i[92]\i[123]Don't count on me to pet you too."
						}
						else
						{
							"\w8%(zhappy)\i[100]\i[92]Yes,\w5 surrender to his cuteness.\n"
							"\w8%(zhappy)\i[100]\i[92]You can't fight his cuteness.\w7%(zgreat)\i[96] Surrender to me!\n"
						}
					}
					
					
					
				}
				useraffection++
				girpets++
 			}
			elseif reference4 == "Antennas"
			{
				"%(ghappyface)\i[92]Hehe."
				"%(ghappyface)\i[2001](He seems amused.)"
			}
 		}
	}
 	else
	{
 		// If the mouse isn't over the ghost, it'll zero out the stroke value.
		gstroke = 0
 	}
}

Select.yesgircute
{
	"\0\i[125]\i[92]Hehehehahaha... %(zbackpose)\i[51]\i[100]\i[125]\i[126]\i[115]\i[116]\i[62]\i[99]\i[147]\i[197]Vict\i[96]ory is mine!!!\w5\n%(zpose)\i[100]\i[151]\i[201]\i[99]\i[115]\i[116]\i[127]\i[130]\i[51]\i[62]Mwah\i[96]ahahahahahahahahaha!!!\n%(zbackpose)\i[62]\i[54]\i[136]\i[137]\i[150]\i[200]\i[99]Mwah\i[96]ahahahahahahahhahahahahaha!!!!!"
}

//---- OnMouseDoubleClick -------------------------------------------------------

OnMouseDoubleClick
 {
 	if reference3 == 0 //if the character is 0, aka your main character
	{
		MouseDoubleClickSakura
 	}
	else
	{
		MouseDoubleClickKero
	}
 }

 //That's all there is to it. This method also applies to the secondary character, Kero, below this set.
 //***********************************************************************
 
MouseDoubleClickSakura : nonoverlap
 {
 	if reference4 == "Head" || reference4 == "Face" //here, this is checking to see if the user is double clicking on either the character's head OR their face, which for our purposes means they're hitting the ghost. More about operators at the walkthrough.
	{ 
		"%(zpose)\i[100]"
		--
		"\0\i[53]\i[42]\i[96]\i[133]\i[134]\i[67]\i[150]\i[200]Ahow!!!\n"
		"\0\i[53]\i[42]\i[133]\i[96]\i[134]\i[67]\i[150]\i[200]Owh!!!\n"
		"\0\i[53]\i[42]\i[133]\i[96]\i[134]\i[67]\i[150]\i[200]Ouch!!!\n"
		"\0\i[53]\i[42]\i[133]\i[96]\i[134]\i[67]\i[150]\i[200]Humph!!!\n"
		--
		if discovered != 1
		{
			"\n[half]\1%(gsadface)\i[149]\i[199]"
			--
			"Master!!! "
			"Master! "
			"Master... "
			"Mmh!!!"
			""
			--			
			if zimpunched > 15
			{
				"\0\i[115]\i[116]\i[123]\i[124]You freak!"
				"\0\i[117]\i[118]\i[117]\i[116](He shivers.)"
				"\0\i[115]\i[116]\i[123]\i[124]Monstruosity."
			}
			elseif zimpunched > 10
			{
				"\0\i[92]"
				--
				"\i[141]\i[191]"
				"\i[145]\i[195]"
				--
				"You're a very cruel human."
				"\i[123]\i[124]\i[115]\i[116]What a cruel human."
			}
			elseif zimpunched > 6
			{
				"\0"
				--
				"\i[92]\i[141]\i[191]You're just as mean as the other humans."
				"\i[75\i[116]\i[129]\i[130]\i[92]\i[141]\i[191]Dib already did worst."
				"\i[112]\i[114]\i[123]\i[124]\i[92]\i[145]\i[195]I'll make you pay this!"
				"\i[112]\i[114]\i[123]\i[124]\i[96]\i[145]\i[195]I'll destroy you!"
			}
			else
			{
				"\0\i[52]\i[112]\i[114]\i[123]\i[124]\i[96]"
				--
				"\i[150]\i[200]"
				"\i[147]\i[197]"
				--
				"\i[112]\i[114]\i[123]\i[124]\i[96]You filthy human! How dare you hitting me!"
				"\i[112]\i[114]\i[123]\i[124]\i[96]You filthy human! How dare you!"
				"\i[112]\i[114]\i[123]\i[124]\i[96]How dare you!!!"
			}
		}
		else
		{
			"\n[half]\1"
			"\n[half]\1Oh no! "
			"\n[half]\1Oh! "
			--
			"Master!!! "
			"Master! "
			"Master... "
			--
			"Are you alright?"
			"Are you ok?"
			""
			--
			
			if zimpunched > 15
			{
				"\0\i[115]\i[116]\i[129]\i[130](He doesn't respond.)"
				"\0\i[115]\i[116]\i[129]\i[130]Are you sure to be human?"
				"\0\i[115]\i[116]\i[123]\i[124]You freak!"
				"\0\i[117]\i[118]\i[117]\i[116](He shivers.)"
				"\0\i[115]\i[116]\i[123]\i[124]Monstruosity."
			}
			elseif zimpunched > 10
			{
				"\0\i[92]"
				--
				"\i[141]\i[191]"
				"\i[145]\i[195]"
				--
				"You're a very cruel human."
				"\i[123]\i[124]\i[115]\i[116]What a cruel human."
				"\i[115]\i[116]\i[129]\i[130]I didn't know humans could be so mean."
			}
			elseif zimpunched > 6
			{
				"\0"
				--
				"\i[150]\i[200]"
				"\i[145]\i[195]"
				--
				"\i[92]You're just as mean as the other humans."
				"\i[75\i[116]\i[129]\i[130]\i[92]Dib already did worst."
				"\i[112]\i[114]\i[123]\i[124]\i[92]I'll make you pay this!"
				"\i[112]\i[114]\i[123]\i[124]\i[96]I'll destroy you!"
			}
			else
			{
				"\0\i[52]\i[112]\i[114]\i[123]\i[124]\i[96]"
				--
				"\i[150]\i[200]"
				"\i[147]\i[197]"
				--
				"You filthy human! I'll destroy you and all your kind!"
				"\i[112]\i[114]\i[123]\i[124]\i[96]You filthy human! How dare you hit me!"
				"\i[112]\i[114]\i[123]\i[124]\i[96]You filthy human! How dare you!"
				"\i[112]\i[114]\i[123]\i[124]\i[96]How dare you!!!"
			}
		}
		zimpunched++
		useraffection--
	}
	else
 	{
		//Otherwise, if the user doubleclicks somewhere else on the character, they will open their menu as detailed in menu.dic.
 		OpenSakuraMenu
	}
 }

 
 //---- MouseDoubleClickKero -----------------------------------------------------------
 //This is just the same as MouseDoubleClickSakura above, except for your secondary character. The same principles apply, including how to make your character unhittable.
 
 
MouseDoubleClickKero : nonoverlap
 {
	if reference4 == "Head" || reference4 == "Face" //here, this is checking to see if the user is double clicking on either the character's head OR their face, which for our purposes means they're hitting the ghost. More about operators at the walkthrough.
	{
		"\1%(gsadface)\i[152]\i[202]Ow!!"
		"\1\i[53]\i[136]\i[137]\i[152]\i[202]Aow aow aow!!"
		"\1%(gsadface)\i[152]\i[202]Ouch!!"
		"\1%(gsadface)\i[152]\i[202]Ahow!!"
		--
		"\n%(gcryface)"
		--
		"\w7Why?"
		"\w7It hurts!"
		"\w7Sob."
		""
		--
		if worried != -1
		{
			"\w8%(zrandompose)\i[100]%(zworriedface)\i[144]\i[194]Why have you done this?"
			"\w8%(zrandompose)\i[100]%(zworriedface)\i[144]\i[194]Please don't hit him,\w7 it makes him cry."
			if discovered != 1
			{
				"\w8%(zrandompose)\i[100]%(zwondering)Are human supposed to be violent with their pets?"
				"\w8%(zrandompose)\i[100]%(zwondering)Are human supposed to be violent with dogs?"
			}
			else
			{
				"\w8%(zrandompose)\i[100]%(zboredface)%(zrelaxedarms)I would like keeping him intact."
				"\w8%(zrandompose)\i[100]%(zboredface)%(zwonderingarms)Could you avoid hitting him? \w7%(zrelaxedarms)He could be useful."
			}
			
		}
	}
	else
	{
		OpenKeroMenu
	}
	
 }

